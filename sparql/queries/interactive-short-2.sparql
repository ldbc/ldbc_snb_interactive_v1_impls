PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT
    ?messageId
    ?messageContent
    ?messageCreationDate
    ?originalPostId
    ?originalPostAuthorId
    ?originalPostAuthorFirstName
    ?originalPostAuthorLastName
WHERE {
    BIND( $personId AS ?personId )
    ?person a snvoc:Person .
    ?person snvoc:id ?personId .
    ?message snvoc:hasCreator ?person .
    ?message snvoc:content|snvoc:imageFile ?messageContent .
    ?message snvoc:creationDate ?messageCreationDate .
    ?message snvoc:id ?messageId .
    OPTIONAL {
        ?message snvoc:replyOf* ?originalPostInner .
        ?originalPostInner a snvoc:Post .
    } .
    BIND( COALESCE(?originalPostInner, ?message) AS ?originalPost ) .
    ?originalPost snvoc:id ?originalPostId .
    ?originalPost snvoc:hasCreator ?creator .
    ?creator snvoc:firstName ?originalPostAuthorFirstName .
    ?creator snvoc:lastName ?originalPostAuthorLastName .
    ?creator snvoc:id ?originalPostAuthorId .
}
ORDER BY DESC(?messageCreationDate) DESC(?messageId)
LIMIT 10

