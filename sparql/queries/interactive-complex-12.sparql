PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT
    (?frId AS ?personId)
    (?first AS ?personFirstName)
    (?last AS ?personLastName)
    (GROUP_CONCAT(DISTINCT ?tagName;separator=', ') AS ?tagNames)
    (COUNT(DISTINCT ?reply) AS ?replyCount)
WHERE
{
    ?rootPerson a snvoc:Person .
    ?rootPerson snvoc:id $personId .
    ?rootPerson ((snvoc:knows/snvoc:hasPerson)|^(snvoc:knows/snvoc:hasPerson)) ?fr .
    ?fr snvoc:id ?frId .
    ?fr snvoc:firstName ?first .
    ?fr snvoc:lastName ?last .
    ?reply snvoc:hasCreator ?fr .
    ?reply snvoc:replyOf ?post .
    ?post a snvoc:Post .
    ?post snvoc:hasTag ?tag .
    ?tag foaf:name ?tagName .
    ?tag a ?tagType.
    ?tagType rdfs:subClassOf* ?tagSuperType .
    ?tagSuperType rdfs:label $tagClassName .
}
GROUP BY ?frId ?first ?last
ORDER BY DESC(?replyCount) ?frId
LIMIT 20
