PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT
    (?fr AS ?personId)
    (?firstName AS ?personFirstName)
    (?lastName AS ?personLastName)
    ?xCount
    ?yCount
    (?xCount + ?yCount AS ?count)
WHERE
{
    {
        SELECT DISTINCT
            ?fr
            ?frId
        WHERE
        {
            ?rootPerson a snvoc:Person .
            ?rootPerson snvoc:id $personId .
            ?rootPerson ((snvoc:knows/snvoc:hasPerson)|^(snvoc:knows/snvoc:hasPerson))?/((snvoc:knows/snvoc:hasPerson)|^(snvoc:knows/snvoc:hasPerson)) ?fr .
            ?fr snvoc:id ?frId .
            FILTER(?fr != ?rootPerson)
        }
    }
    {
        SELECT
            (?frId AS ?frIdX)
            (COUNT(*) AS ?xCount)
        WHERE
        {
            BIND( $startDate AS ?date1 ) .
            BIND( ?date1 + "P$durationDaysD"^^xsd:duration AS ?date2 ) .
            VALUES (?type) {(snvoc:Comment) (snvoc:Post)}
            ?message a ?type .
            ?message snvoc:creationDate ?creationDate .
            FILTER( ?creationDate >= ?date1 && ?creationDate < ?date2 ) .
            ?message snvoc:hasCreator ?fr .
            ?fr a snvoc:Person .
            ?fr snvoc:id ?frId .
            ?message snvoc:isLocatedIn ?country .
            ?country a dbpedia-owl:Country .
            ?country foaf:name $countryXName .
        }
        GROUP BY ?frId
    } .
    FILTER( ?frId = ?frIdX ) .
    {
        SELECT
            (?frId AS ?frIdY)
            (COUNT(*) AS ?yCount)
        WHERE
        {
            BIND( $startDate AS ?date1 ) .
            BIND( ?date1 + "P$durationDaysD"^^xsd:duration AS ?date2 ) .
            VALUES (?type) {(snvoc:Comment) (snvoc:Post)}
            ?message a ?type .
            ?message snvoc:creationDate ?creationDate .
            FILTER( ?creationDate >= ?date1 && ?creationDate < ?date2 ) .
            ?message snvoc:hasCreator ?fr .
            ?fr a snvoc:Person .
            ?fr snvoc:id ?frId .
            ?message snvoc:isLocatedIn ?country .
            ?country a dbpedia-owl:Country .
            ?country foaf:name $countryYName .
        }
        GROUP BY ?frId
    } .
    FILTER( ?frId = ?frIdY )
    ?fr snvoc:firstName ?firstName .
    ?fr snvoc:lastName ?lastName
}
ORDER BY DESC(?sum) ?fr
LIMIT 20

