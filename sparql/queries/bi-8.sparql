PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT ?relatedTagName (COUNT(?comment) AS ?count)
WHERE
{
    BIND ($tag AS ?tagName)
    VALUES (?type) {(snvoc:Comment) (snvoc:Post)}
    ?message a ?type .
    ?message snvoc:hasTag/foaf:name ?tagName .
    ?comment snvoc:replyOf ?message .
    ?comment snvoc:hasTag/foaf:name ?relatedTagName .
    FILTER NOT EXISTS {?comment snvoc:hasTag/foaf:name ?tagName}

}
GROUP BY ?relatedTagName
ORDER BY DESC(?count) ?relatedTagName
LIMIT 100

